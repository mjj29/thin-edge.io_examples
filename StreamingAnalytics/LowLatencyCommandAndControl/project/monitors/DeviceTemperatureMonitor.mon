/*
 * Copyright (c) 2022 Cumulocity GmbH, Duesseldorf, Germany and/or its affiliates and/or their licensors.
 * Use, reproduction, transfer, publication or disclosure is prohibited except as specifically provided for in your License Agreement with Cumulocity GmbH.
 * 
 */

package com.apamax.thinedge.lowlatencysample;

using com.apama.aggregates.mean;
/** This monitor is responsible for sending a shutdown alert if the average temperature in the last 10 seconds exceeds a threshold temperature (in this example - 80C). */

monitor DeviceTemperatureMonitor {
	
    constant float TEN_SECS_INTERVAL := 10.0;
    constant float THRESHOLD_TEMPERATURE := 80.0;
    
    action onload() {
       monitor.subscribe(Temperature.SUBSCRIBE_CHANNEL);
       stream<Temperature> temperatures := all Temperature();
       from temp in temperatures within TEN_SECS_INTERVAL
         select Temperature(mean(temp.value)) as temperature{
           if temperature.value > THRESHOLD_TEMPERATURE {
                //log temperature.value.toString() at INFO;
                log "Average temperature " + temperature.value.toString() + " Celsius" + " exceeded threshold temperature " + THRESHOLD_TEMPERATURE.toString() + " Celsius. Sending alert and requesting shutdown." at WARN;
                send ShutDown("Device temperature is over the threshold value. Shutdown the device now.") to ShutDown.SEND_CHANNEL;
                send Temperature(temperature.value) to Temperature.SEND_CHANNEL;
           }
        }
    }
}
